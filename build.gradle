plugins {
	id 'org.springframework.boot' version '2.4.5'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'io.openapiprocessor.openapi-processor' version '1.0.0.M9'
}

group = 'com.xarun'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-websocket'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	// for sending emails
	implementation "org.springframework.boot:spring-boot-starter-mail:2.4.0"
	implementation "org.springframework:spring-context-support:5.3.1"

	// for JPA
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

	// for validation annotations
	implementation 'org.hibernate.validator:hibernate-validator'
	implementation 'org.hibernate.validator:hibernate-validator-annotation-processor'

	// for autoMapping
	def mapstructVersion = "1.3.1.Final"
	implementation "org.mapstruct:mapstruct:${mapstructVersion}"
	annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"

	implementation 'org.flywaydb:flyway-core'
	runtimeOnly 'mysql:mysql-connector-java'
}

openapiProcessor {
	apiPath "${projectDir}/src/api/openapi.yaml"

	spring {
		processor 'io.openapiprocessor:openapi-processor-spring:1.0.0.M20'
		targetDir "${projectDir}/build/openapi"
		mapping "${projectDir}/src/api/mapping.yaml"
		showWarnings true
	}

	json {
		processor 'io.openapiprocessor:openapi-processor-json:1.0.0.M6'
		targetDir "${buildDir}/json"
	}

}

sourceSets {
	main {
		java {
			srcDir 'build/openapi'
		}
	}
}

compileJava.dependsOn('processSpring')


test {
	useJUnitPlatform()
}
